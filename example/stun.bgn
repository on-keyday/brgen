
enum MessageType:
    :u16
    BindingRequest = 0x0001
    BindingResponse = 0x0101
    BindingErrorResponse = 0x0111
    SharedSecretRequest = 0x0002
    SharedSecretResponse = 0x0102
    SharedSecretErrorResponse = 0x0112

enum StunAddrFamily:
    :u8
    IPv4 = 0x01
    IPv6 = 0x02

MAGIC_COOKIE ::= 0x2112A442

format StunHeader:
    msg_type :MessageType
    length :u16
    magic_cookie :u32(MAGIC_COOKIE)
    transaction_id :[12]u8

format StunPacket:
    header :StunHeader
    attributes :[..]Attribute(input = input.subrange(header.length))

format Attribute:
    type :AttributeType
    length :u16
    match type:
        AttributeType.mapped_address:
            mapped_address :MappedAddress(input = input.subrange(length))
        AttributeType.change_request:
            change_request :ChangeRequest(input = input.subrange(length))
        AttributeType.error_code:
            error_code :ErrorCode(input = input.subrange(length))
        AttributeType.xor_mapped_address:
            xor_mapped_address :MappedAddress(input = input.subrange(length))
        ..:
            data :[length]u8

format MappedAddress:
    padding :u8
    family :StunAddrFamily
    family == StunAddrFamily.IPv4 || family == StunAddrFamily.IPv6
    port :u16
    address :[family == StunAddrFamily.IPv4 ? 4: 16]u8

format ChangeRequest:
    reserved1 :u29
    change_ip :u1
    change_port :u1
    reserved2 :u1

format ErrorCode:
    reserved :u22
    class :u3
    number :u7
   
enum AttributeType:
    :u16
    reserved = 0x0000
    mapped_address = 0x0001
    response_address = 0x0002
    change_request = 0x0003
    source_address = 0x0004
    changed_address = 0x0005
    user_name = 0x0006
    password = 0x0007
    message_integrity = 0x0008
    error_code = 0x0009
    unknown_attribute = 0x000a
    reflected_from = 0x000b
    realm = 0x0014
    nonce = 0x0015
    xor_mapped_address = 0x0020
