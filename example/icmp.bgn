config.go.package = "ether"

format ICMPHeader:
    type: u8
    code: u8
    checksum: u16

format ICMPTimeExceeded:
    unused1: u8
    length: u8
    unused2: u16
    data: [..]u8

format ICMPEcho:
    id: u16
    seq: u16
    data: [..]u8

format ICMPEchoReply:
    id: u16
    seq: u16
    data: [..]u8

format ICMPDestinationUnreachable:
    unused: u16
    next_hop_mtu: u16
    data: [..]u8

format NDPRouterSolicitation:
    reserved: u32
    options : [..]NDPOption

format NDPRouterAdvertisement:
    cur_hop_limit: u8
    m :u1
    o :u1
    reserved: u6
    router_lifetime: u16
    reachable_time: u32
    retrans_timer: u32
    options: [..]NDPOption

format NDPNeighborSolicitation:
    target_addr: [16]u8
    options: [..]NDPOption

format NDPNeighborAdvertisement:
    target_addr: [16]u8
    flags: u32
    options: [..]NDPOption

format NDPRedirectMessage:
    target_addr: [16]u8
    dest_addr: [16]u8
    options: [..]NDPOption

format ICMPv4Packet:
    header: ICMPHeader
    match ICMPv4Type(header.type):
        ICMPv4Type.echo:
            echo: ICMPEcho
        ICMPv4Type.echo_reply:
            echo_reply: ICMPEchoReply
        ICMPv4Type.time_exceeded:
            time_exceeded: ICMPTimeExceeded
        ICMPv4Type.dst_unreachable:
            destination_unreachable: ICMPDestinationUnreachable
        ..:
            data: [..]u8

format ICMPv6Packet:
    header: ICMPHeader
    match ICMPv6Type(header.type):
        ICMPv6Type.router_solicitation:
            router_solicitation: NDPRouterSolicitation
        ICMPv6Type.router_advertisement:
            router_advertisement: NDPRouterAdvertisement
        ICMPv6Type.neighbor_solicitation:
            neighbor_solicitation: NDPNeighborSolicitation
        ICMPv6Type.neighbor_advertisement:
            neighbor_advertisement: NDPNeighborAdvertisement
        ..:
            data: [..]u8


enum NDPOptionType:
    :u8
    source_link_layer_address = 1
    target_link_layer_address = 2
    prefix_information = 3
    redirect_header = 4
    mtu = 5

format NDPOption:
    type: NDPOptionType
    length: u8
    length != 0
    match NDPOptionType(type):
        NDPOptionType.source_link_layer_address:
            link_layer_address: [length*8 - 2]u8
        NDPOptionType.target_link_layer_address:
            link_layer_address: [length*8 - 2]u8
        NDPOptionType.prefix_information:
            prefix_information: PrefixInformation(input = input.subrange(length*8 - 2))
        NDPOptionType.redirect_header:
            redirect_header: RedirectHeader(input = input.subrange(length*8 - 2))
        NDPOptionType.mtu:
            mtu: MTU(input = input.subrange(length*8 - 2))
        ..:
            data: [length * 8 - 2]u8

format PrefixInformation:
    prefix_length: u8
    l :u1
    a :u1
    reserved1: u6
    valid_lifetime: u32
    preferred_lifetime: u32
    reserved2: u32
    prefix: [16]u8

format RedirectHeader:
    reserved :[6]u8
    hdr_and_data: [..]u8

format MTU:
    reserved: u16
    mtu: u32

enum ICMPv4Type:
    :u8
    echo_reply
    reserved_1
    reserved_2
    dst_unreachable
    src_quench
    redirect
    alt_host_address
    reserved_3
    echo
    router_advertisement
    router_solicitation
    time_exceeded
    parameter_problem
    timestamp
    timestamp_reply
    information_request
    information_reply
    address_mask_request
    address_mask_reply
    reserved_for_security_1
    reserved_for_robustness_experiment_1
    reserved_for_robustness_experiment_2
    reserved_for_robustness_experiment_3
    reserved_for_robustness_experiment_4
    reserved_for_robustness_experiment_5
    reserved_for_robustness_experiment_6
    reserved_for_robustness_experiment_7
    reserved_for_robustness_experiment_8
    reserved_for_robustness_experiment_9
    reserved_for_robustness_experiment_10
    traceroute
    datagram_conversion_error
    mobile_host_redirect
    where_are_you
    here_I_am
    mobile_registration_request
    mobile_registration_reply
    domain_name_request
    domain_name_reply
    skip_discovery
    security_failure
    icmp_for_seamoby
    experimental_1 = 253
    experimental_2 = 255

enum ICMPv4Code:
    :u8
    net_unreachable
    host_unreachable
    protocol_unreachable
    port_unreachable
    fragmentation_needed
    source_route_failed
    network_unknown
    host_unknown
    network_prohibited
    host_prohibited
    TOS_network_unreachable
    TOS_host_unreachable
    communication_prohibited
    host_precedence_violation
    precedence_cutoff

enum ICMPv6Type:
    :u8
    destination_unreachable = 1
    packet_too_big = 2
    time_exceeded = 3
    parameter_problem = 4
    echo_request = 128
    echo_reply = 129
    multicast_listener_query = 130
    multicast_listener_report = 131
    multicast_listener_done = 132
    router_solicitation = 133
    router_advertisement = 134
    neighbor_solicitation = 135
    neighbor_advertisement = 136
    redirect_message = 137
    router_renumbering = 138
    node_information_query = 139
    node_information_response = 140 
    inverse_neighbor_discovery_solicitation = 141
    inverse_neighbor_discovery_advertisement = 142
    v2_multicast_listener_report = 143
    home_agent_address_discovery_request = 144
    home_agent_address_discovery_reply = 145
    mobile_prefix_solicitation = 146
    mobile_prefix_advertisement = 147
    certification_path_solicitation = 148
    certification_path_advertisement = 149
    experimental_mobility_protocols = 253
    experimental_mobility_protocols_2 = 254
    reserved = 255

enum ICMPv6Code:
    :u8
    no_route_to_dst 
    communication_with_dst_admin_prohibited
    beyond_scope_of_src
    address_unreachable
    port_unreachable
    src_addr_failed_ingress_egress_policy
    reject_route_to_dst
    error_in_src_routing_header
    rejected_dst_addr_unreachable
    error_in_dst_options_header
