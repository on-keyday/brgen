
config.cpp.namespace = "futils::fnet::http2"

enum H2Error:
    none = 0x0
    protocol = 0x1
    internal = 0x2
    flow_control = 0x3
    settings_timeout = 0x4
    stream_closed = 0x5
    frame_size = 0x6
    refused = 0x7
    cancel = 0x8
    compression = 0x9
    connect = 0xa
    enhance_your_clam = 0xb
    inadequate_security = 0xc
    http_1_1_required = 0xd
    transport = 0x100

enum H2Type:
    :u8
    DATA = 0x0
    HEADERS = 0x1
    PRIORITY = 0x2
    RST_STREAM = 0x3
    SETTINGS = 0x4
    PUSH_PROMISE = 0x5
    PING = 0x6
    GOAWAY = 0x7
    WINDOW_UPDATE = 0x8
    CONTINUATION = 0x9


format FrameHeader:
    len :u24
    type:H2Type
    reserved_flag :u4(0)
    padding :u1
    end_headers :u1
    unused :u1
    end_stream_or_ack :u1 #TODO(on-keyday): rename? make it more clear
    reserved :u1
    stream_id:u31

format DataFrame:
    header :FrameHeader
    data :[header.len]u8

format StreamWeight:
    exclusive :u1
    stream_id :u31
    weight :u8


format HeadersFrame:
    header :FrameHeader
    priority :StreamWeight
    if header.padding == 1:
        padding_len :u8
        u32(header.len) > u32(padding_len + 1)
    header_block :[u32(header.len) - u32(available(padding_len)?padding_len:0)]u8
    if header.padding == 1:
        padding :[padding_len]u8

format PriorityFrame:
    header :FrameHeader
    priority :StreamWeight

format RstStreamFrame:
    header :FrameHeader
    error_code :u32

format Setting:
    id :u16
    value :u32

format SettingsFrame:
    header :FrameHeader
    header.len % 6 == 0
    if header.end_stream_or_ack == 1:
        header.len == 0
        return
    settings :[header.len / 6]Setting

format PushPromiseFrame:
    header :FrameHeader
    promised_stream_id :u31
    header_block :[header.len - 4]u8

format PingFrame:
    header :FrameHeader
    header.len == 8
    opaque_data :u64

format GoAwayFrame:
    header :FrameHeader
    reserved :u1(0)
    last_stream_id :u31
    error_code :u32
    debug_data :[header.len - 8]u8

format WindowUpdateFrame:
    header :FrameHeader
    reserved :u1(0)
    window_size_increment :u31

format ContinuationFrame:
    header :FrameHeader
    header_block :[header.len]u8

    